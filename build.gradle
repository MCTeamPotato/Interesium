plugins {
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
}

base {
    archivesName = "${archives_base_name}"
}

version = "${minecraft_version}-${mod_version}"
group = "${maven_group}"

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
            "interesium.mixins.json"
        ]
    }
}

repositories {
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.parchmentmc.org' }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    //mappings "net.fabricmc:yarn:${minecraft_version}+build.${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    modImplementation(files("deps/minecraft-comes-alive-7.3.23+1.16.5-forge.jar"))
    modImplementation "me.shedaniel:architectury-forge:1.32.68"
    modImplementation "curse.maven:atum-59621:3990329"
    modImplementation "curse.maven:blue-skies-312918:3411324"
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(8)
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Implementation-Title"    : archives_base_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}